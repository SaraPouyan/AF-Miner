FUNCTION_BLOCK FUF

// Define input variables
VAR_INPUT
    itrNum : REAL;
    improvementRatio : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    Weight : REAL;
END_VAR

// Fuzzify input variable 'itrNum'
FUZZIFY itrNum
      TERM low  := Gauss2 0.0119 0.0 0.39 0.13;
      TERM mid  := Gauss2 0.763 0.139  0.837 0.05349;
      TERM high := Gauss2  1.01 0.0599  1.02 0.0;
END_FUZZIFY

// Fuzzify input variable 'improvementRatio'
FUZZIFY improvementRatio
      TERM low  := Gauss2 0.0 0.0 0.04 0.1159;
      TERM mid  := Gauss2 0.46 0.1159 0.54 0.1159;
      TERM high := Gauss2 0.96 0.1159  1.0 0.0;
END_FUZZIFY

// Defzzzify output variable 'Weight'
DEFUZZIFY Weight
    TERM veryLow  := (0.7,0) (0.8,1) (0.84,1) (0.89,0);
    TERM low      := (0.84,0) (0.88,0.93) (0.89,0.9) (0.925,0);
    TERM mid      := (0.883,0) (0.915,0.9) (0.925,0.96) (0.985,0);
    TERM high     := (0.916,0) (0.96,0.88) (0.97,0.96) (1,0);
    TERM veryHigh := (0.98,0) (1,1);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF itrNum IS low  AND improvementRatio IS high THEN Weight IS veryHigh;
    RULE 2 : IF itrNum IS low  AND improvementRatio IS mid  THEN Weight IS high;
    RULE 3 : IF itrNum IS low  AND improvementRatio IS low  THEN Weight IS mid;
    RULE 4 : IF itrNum IS mid  AND improvementRatio IS high THEN Weight IS high;
    RULE 5 : IF itrNum IS mid  AND improvementRatio IS mid  THEN Weight IS mid;
    RULE 6 : IF itrNum IS mid  AND improvementRatio IS low  THEN Weight IS low;
    RULE 7 : IF itrNum IS high AND improvementRatio IS high THEN Weight IS mid;
    RULE 8 : IF itrNum IS high AND improvementRatio IS mid  THEN Weight IS low;
    RULE 9 : IF itrNum IS high AND improvementRatio IS low  THEN Weight IS veryLow;

END_RULEBLOCK

END_FUNCTION_BLOCK